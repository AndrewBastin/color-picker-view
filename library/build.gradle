apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.3.0"

android {
    compileSdkVersion 22
    buildToolsVersion "23.0.0 rc2"
    resourcePrefix "colorpickerview__"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 3
        versionName "1.3"
    }
    buildTypes {
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

// Homepage URL of the library
def siteUrl = 'https://github.com/danielnilsson9/color-picker-view'
// Git repository URL
def gitUrl = 'github-windows://openRepo/https://github.com/danielnilsson9/color-picker-view.git'
// maven group id
group = "bintray-danielnilsson9-maven"


install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'A simple good looking color picker component for Android.'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'danielnilsson9'
                        name 'Daniel Nilsson'
                        email 'daniel.nilsson.9@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}




Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "color-picker-view"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}